# Theme: iRB_Classic
# By: IRD, v1.0, September 2021 (infra_red_dude@hotmail.com)
# Based on iClassic Square By: Humberto Santana (humbertosantana@hotmail.com)
# License: Creative Commons Attribution-Share Alike 3.0 (http://creativecommons.org/licenses/by-sa/3.0/)
# A few elements derived from iLike Theme for 320x240 by Alan Brito, which was ported from the 176x132 version by H. Pearce
# If used for other works, as per this license, please include the following Attribution Text: "Modified/Derived from iClassic Square theme for iPod Video (CC-BY-SA 3.0) by Humberto Santana"
#
# FMS File
#
#-- disable statusbar and load background
%wd
%X(iRB_Classic_bg.bmp)
#
#-- Touchscreen related (N/A for iPod)
%T(0,0,216,18,menu)
%T(0,0,216,18,&cancel)
%T(0,200,58,25,prev)
%T(0,200,58,25,&rwd)
%T(160,132,65,15,presets)
%T(262,200,58,25,next)
%T(262,200,58,25,&ffwd)
%T(240,0,45,18,play)
%T(240,0,45,18,&stop)
%T(58,206,204,23,progressbar)
#
#-- Fonts
%Fl(2,16-Inter-SemiBold.fnt)
%Fl(3,16-NotoSans-Bold.fnt)
#
#-- Title bar text
%?mv(5)<%?St(battery display)<%Vd(title)|%Vd(titlebat)>|%?St(battery display)<%?mp<%Vd(clock)|%Vd(clock)|%Vd(clock)|%Vd(title)|%Vd(title)>|%?mp<%Vd(clock)|%Vd(clock)|%Vd(clock)|%Vd(titlebat)|%Vd(titlebat)>>>
%Vl(title,5,1,246,16,2)%Vf(000000)
%s%al%Sx(FM Radio)
%Vl(titlebat,5,1,211,16,2)%Vf(000000)
%s%al%Sx(FM Radio)
%Vl(clock,5,1,211,16,2)%Vf(000000)
%t(10)%s%al%Sx(FM Radio);%t(30)%s%al%?cf<%cH:%cM|%cl:%cM %cP>
#
#-- Battery images
%xl(B,Battery.bmp,0,0,24)
%V(291,4,25,11,-)
%?bp<%?bc<%t(1.0)%xd(Ba);%t(1.0)%xd(Bb)|%xd(Bc)>|%?bl<%xd(Bd)|%xd(Bd)|%xd(Be)|%xd(Bf)|%xd(Bg)|%xd(Bh)|%xd(Bi)|%xd(Bj)|%xd(Bk)|%xd(Bl)|%xd(Bm)|%xd(Bn)|%xd(Bo)|%xd(Bp)|%xd(Bq)|%xd(Br)|%xd(Bs)|%xd(Bt)|%xd(Bu)|%xd(Bv)|%xd(Bw)|%xd(Bx)||>>
%?St(battery display)<%Vd(nonumbat)|%Vd(numbat)>
#
#-- title bar icons (play/pause/hold etc.)
%Vl(numbat,255,3,32,12,2)%Vf(000000)
%ac%bl%%
%xl(P,Playing Status.bmp,0,0,4)
%Vl(numbat,236,3,16,13,-)
%?mp<|%xd(Pa)|%xd(Pb)|%xd(Pc)|%xd(Pd)|||%xd(Pa)|%xd(Pb)>
%Vl(nonumbat,271,3,16,13,-)
%?mp<|%xd(Pa)|%xd(Pb)|%xd(Pc)|%xd(Pd)|||%xd(Pa)|%xd(Pb)>
%xl(h,Hold Icon.bmp,0,1)
%xl(M,Memory Access.bmp,0,0,6)
%Vl(numbat,218,3,13,13,-)
%?lh<%t(0.1)%xd(Ma);%t(0.1)%xd(Mb);%t(0.1)%xd(Mc);%t(0.1)%xd(Md);%t(0.1)%xd(Me);%t(0.1)%xd(Mf)|%?mh<%xd(h)|>>
%Vl(nonumbat,253,2,13,13,-)
%?lh<%t(0.1)%xd(Ma);%t(0.1)%xd(Mb);%t(0.1)%xd(Mc);%t(0.1)%xd(Md);%t(0.1)%xd(Me);%t(0.1)%xd(Mf)|%?mh<%xd(h)|>>
%xl(s,Stereo Icon.bmp,0,0)
#
#-- album art
%V(295,21,17,14,-)
%?ts<%xd(s)|>
%?C<%Vd(aa)|%Vd(na)>
#%xl(w,FrameTop.bmp,0,0)
#%xl(x,FrameLeft.bmp,0,13)
#%xl(y,FrameRight.bmp,140,13)
#%xl(z,FrameBottom.bmp,0,136)
%Cl(0,0,136,136,c,c)
%Vl(aa,11,45,136,136,-)
#%V(7,39,144,142,-)
%Cd
#
#-- Station info/tuner etc.
%xl(r,Radio Icon.bmp,0,0)
%Vl(na,11,45,136,136,-)
%xd(r)
%V(160,52,157,21,1)%Vf(000000)
%al%tf Mhz
%V(160,77,157,19,3)%Vf(555555)
%s%al%?Tn<%Tn|%?ty<%ty|%?tt<Tuned|tune>>>
#
#-- signal strength text
%V(160,96,157,19,3)%Vf(555555)
%s%al%?tz<%tz|%Sx(Signal strength:)>
#
#-- station rating
%xl(R,Ratings.bmp,0,0,10)
%V(160,116,66,13,-)
%?tr<|%xd(Ra)|%xd(Rb)|%xd(Rc)|%xd(Rd)|%xd(Re)|%xd(Rf)|%xd(Rg)|%xd(Rh)|%xd(Ri)|%xd(Rj)>
#
#-- progress/volume/scanning bar
%V(160,134,157,14,2)%Vf(000000)
%s%al%?tm<%Sx(Scan)|%Ti %Sx(of) %Tc>
%?mv(2.0)<%Vd(volume)|%Vd(pb)>
%Vl(pb,58,206,204,23,-) 
%pb(0,0,204,23,PB.bmp)
%Vl(volume,58,206,204,23,-)
%pv(0,0,204,23,PB.bmp)
%xl(V,Volume Right.bmp,0,0)
%Vl(volume,273,203,21,21,-)
%xd(V)
%xl(v,Volume Left.bmp,0,0)
%Vl(volume,34,204,13,19,-)
%xd(v)
%Vl(volume,58,227,204,13,2)%Vf(000000)
%ac%?St(volume display)<|%pv dB>
%Vl(pb,0,207,45,14,2)%Vf(000000)
%ar%ta
%Vl(pb,273,207,47,14,2)%Vf(000000)
%al%tb